[[query-operators]]
= Operators

* <<query-operators-summary, Operators at a glance>>
* <<query-operators-aggregation, Aggregation operators>>
 ** <<syntax-using-the-distinct-operator, Using the `DISTINCT` operator>>
* <<query-operators-property, Property operators>>
 ** <<syntax-accessing-the-property-of-a-node-or-relationship, Statically accessing a property of a node or relationship using the `.` operator>>
 ** <<syntax-filtering-on-a-dynamically-computed-property-key, Filtering on a dynamically-computed property key using the `[]` operator>>
 ** <<syntax-property-replacement-operator, Replacing all properties of a node or relationship using the `=` operator>>
 ** <<syntax-property-mutation-operator, Mutating specific properties of a node or relationship using the `+=` operator>>
* <<query-operators-mathematical, Mathematical operators>>
 ** <<syntax-using-the-exponentiation-operator, Using the exponentiation operator `^`>>
 ** <<syntax-using-the-unary-minus-operator, Using the unary minus operator `-`>>
* <<query-operators-comparison, Comparison operators>>
 ** <<syntax-comparing-two-numbers, Comparing two numbers>>
 ** <<syntax-using-starts-with-to-filter-names, Using `STARTS WITH` to filter names>>
 ** <<cypher-comparison, Equality and comparison of values>>
 ** <<cypher-ordering, Ordering and comparison of values>>
 ** <<cypher-operations-chaining, Chaining comparison operations>>
* <<query-operators-boolean, Boolean operators>>
 ** <<syntax-using-boolean-operators-to-filter-numbers, Using boolean operators to filter numbers>>
* <<query-operators-string, String operators>>
 ** <<syntax-using-a-regular-expression-to-filter-words, Using a regular expression with `=~` to filter words>>
* <<query-operators-temporal, Temporal operators>>
 ** <<syntax-add-subtract-duration-to-temporal-instant, Adding and subtracting a _Duration_ to or from a temporal instant>>
 ** <<syntax-add-subtract-duration-to-duration, Adding and subtracting a _Duration_ to or from another _Duration_>>
 ** <<syntax-multiply-divide-duration-number, Multiplying and dividing a _Duration_ with or by a number>>
* <<query-operators-map, Map operators>>
 ** <<syntax-accessing-the-value-of-a-nested-map, Statically accessing the value of a nested map by key using the `.` operator">>
 ** <<syntax-accessing-dynamic-map-parameter, Dynamically accessing the value of a map by key using the `[]` operator and a parameter>>
* <<query-operators-list, List operators>>
 ** <<syntax-concatenating-two-lists, Concatenating two lists using `+`>>
 ** <<syntax-using-in-to-check-if-a-number-is-in-a-list, Using `IN` to check if a number is in a list>>
 ** <<syntax-using-in-for-more-complex-list-membership-operations, Using `IN` for more complex list membership operations>>
 ** <<syntax-accessing-elements-in-a-list, Accessing elements in a list using the `[]` operator>>
 ** <<syntax-accessing-element-in-a-list-parameter, Dynamically accessing an element in a list using the `[]` operator and a parameter>>
 ** <<syntax-using-in-with-nested-list-subscripting, Using `IN` with `[]` on a nested list>>
      

[[query-operators-summary]]
== Operators at a glance


[subs=none]
|===
| <<query-operators-aggregation, Aggregation operators>> | `DISTINCT`
| <<query-operators-property, Property operators>> | `.` for static property access, `[]` for dynamic property access, `=` for replacing all properties, `+=` for mutating specific properties
| <<query-operators-mathematical, Mathematical operators>> | `+`, `-`, `*`, `/`, `%`, `^`
| <<query-operators-comparison, Comparison operators>>     | `=`, `<>`, `<`, `>`, `+<=+`, `>=`, `IS NULL`, `IS NOT NULL`
| <<query-operators-comparison, String-specific comparison operators>> | `STARTS WITH`, `ENDS WITH`, `CONTAINS`
| <<query-operators-boolean, Boolean operators>> | `AND`, `OR`, `XOR`, `NOT`
| <<query-operators-string, String operators>>   | `+` for concatenation, `=~` for regex matching
| <<query-operators-temporal, Temporal operators>>   | `+` and `-` for operations between durations and temporal instants/durations, `*` and `/` for operations between durations and numbers
| <<query-operators-map, Map operators>>       |  `.` for static value access by key, `[]` for dynamic value access by key
| <<query-operators-list, List operators>>       | `+` for concatenation, `IN` to check existence of an element in a list, `[]` for accessing element(s) dynamically
|===


[[query-operators-aggregation]]
== Aggregation operators

The aggregation operators comprise:

* remove duplicates values: `DISTINCT`

[[syntax-using-the-distinct-operator]]
=== Using the `DISTINCT` operator

Retrieve the unique eye colors from `Person` nodes.


.Query
[source, cypher]
----
CREATE
  (a:Person {name: 'Anne', eyeColor: 'blue'}),
  (b:Person {name: 'Bill', eyeColor: 'brown'}),
  (c:Person {name: 'Carol', eyeColor: 'blue'})
WITH [a, b, c] AS ps
UNWIND ps AS p
RETURN DISTINCT p.eyeColor
----

Even though both *'Anne'* and *'Carol'* have blue eyes, *'blue'* is only returned once.

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +p.eyeColor+
| +"blue"+
| +"brown"+
1+d|Rows: 2 +
Nodes created: 3 +
Properties set: 6 +
Labels added: 3
|===

ifndef::nonhtmloutput[]
[subs="none"]
++++
<formalpara role="cypherconsole">
<title>Try this query live</title>
<para><database><![CDATA[
none
]]></database><command><![CDATA[
CREATE
  (a:Person {name: 'Anne', eyeColor: 'blue'}),
  (b:Person {name: 'Bill', eyeColor: 'brown'}),
  (c:Person {name: 'Carol', eyeColor: 'blue'})
WITH [a, b, c] AS ps
UNWIND ps AS p
RETURN DISTINCT p.eyeColor
]]></command></para></formalpara>
++++
endif::nonhtmloutput[]

`DISTINCT` is commonly used in conjunction with <<query-functions-aggregating,aggregating functions>>.

[[query-operators-property]]
== Property operators

The property operators pertain to a node or a relationship, and comprise:

* statically access the property of a node or relationship using the dot operator: `.`
* dynamically access the property of a node or relationship using the subscript operator: `[]`
* property replacement `=` for replacing all properties of a node or relationship
* property mutation operator `+=` for setting specific properties of a node or relationship

[[syntax-accessing-the-property-of-a-node-or-relationship]]
=== Statically accessing a property of a node or relationship using the `.` operator


.Query
[source, cypher]
----
CREATE
  (a:Person {name: 'Jane', livesIn: 'London'}),
  (b:Person {name: 'Tom', livesIn: 'Copenhagen'})
WITH a, b
MATCH (p:Person)
RETURN  p.name
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +p.name+
| +"Jane"+
| +"Tom"+
1+d|Rows: 2 +
Nodes created: 2 +
Properties set: 4 +
Labels added: 2
|===

ifndef::nonhtmloutput[]
[subs="none"]
++++
<formalpara role="cypherconsole">
<title>Try this query live</title>
<para><database><![CDATA[
none
]]></database><command><![CDATA[
CREATE
  (a:Person {name: 'Jane', livesIn: 'London'}),
  (b:Person {name: 'Tom', livesIn: 'Copenhagen'})
WITH a, b
MATCH (p:Person)
RETURN  p.name
]]></command></para></formalpara>
++++
endif::nonhtmloutput[]

[[syntax-filtering-on-a-dynamically-computed-property-key]]
=== Filtering on a dynamically-computed property key using the `[]` operator


.Query
[source, cypher]
----
CREATE
  (a:Restaurant {name: 'Hungry Jo', rating_hygiene: 10, rating_food: 7}),
  (b:Restaurant {name: 'Buttercup Tea Rooms', rating_hygiene: 5, rating_food: 6}),
  (c1:Category {name: 'hygiene'}),
  (c2:Category {name: 'food'})
WITH a, b, c1, c2
MATCH (restaurant:Restaurant), (category:Category)
WHERE restaurant["rating_" + category.name] > 6
RETURN DISTINCT restaurant.name
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +restaurant.name+
| +"Hungry Jo"+
1+d|Rows: 1 +
Nodes created: 4 +
Properties set: 8 +
Labels added: 4
|===

ifndef::nonhtmloutput[]
[subs="none"]
++++
<formalpara role="cypherconsole">
<title>Try this query live</title>
<para><database><![CDATA[
none
]]></database><command><![CDATA[
CREATE
  (a:Restaurant {name: 'Hungry Jo', rating_hygiene: 10, rating_food: 7}),
  (b:Restaurant {name: 'Buttercup Tea Rooms', rating_hygiene: 5, rating_food: 6}),
  (c1:Category {name: 'hygiene'}),
  (c2:Category {name: 'food'})
WITH a, b, c1, c2
MATCH (restaurant:Restaurant), (category:Category)
WHERE restaurant["rating_" + category.name] > 6
RETURN DISTINCT restaurant.name
]]></command></para></formalpara>
++++
endif::nonhtmloutput[]

See <<query-where-basic>> for more details on dynamic property access.

[NOTE]
====
The behavior of the `[]` operator with respect to `null` is detailed <<cypher-null-bracket-operator, here>>.


====

[[syntax-property-replacement-operator]]
=== Replacing all properties of a node or relationship using the `=` operator


.Query
[source, cypher]
----
CREATE (a:Person {name: 'Jane', age: 20})
WITH a
MATCH (p:Person {name: 'Jane'})
SET p = {name: 'Ellen', livesIn: 'London'}
RETURN p.name, p.age, p.livesIn
----

All the existing properties on the node are replaced by those provided in the map; i.e. the `name` property is updated from `Jane` to `Ellen`, the `age` property is deleted, and the `livesIn` property is added.

.Result
[role="queryresult",options="header,footer",cols="3*<m"]
|===
| +p.name+ | +p.age+ | +p.livesIn+
| +"Ellen"+ | +<null>+ | +"London"+
3+d|Rows: 1 +
Nodes created: 1 +
Properties set: 5 +
Labels added: 1
|===

ifndef::nonhtmloutput[]
[subs="none"]
++++
<formalpara role="cypherconsole">
<title>Try this query live</title>
<para><database><![CDATA[
none
]]></database><command><![CDATA[
CREATE (a:Person {name: 'Jane', age: 20})
WITH a
MATCH (p:Person {name: 'Jane'})
SET p = {name: 'Ellen', livesIn: 'London'}
RETURN p.name, p.age, p.livesIn
]]></command></para></formalpara>
++++
endif::nonhtmloutput[]

See <<set-replace-properties-using-map>> for more details on using the property replacement operator `=`.

[[syntax-property-mutation-operator]]
=== Mutating specific properties of a node or relationship using the `+=` operator


.Query
[source, cypher]
----
CREATE (a:Person {name: 'Jane', age: 20})
WITH a
MATCH (p:Person {name: 'Jane'})
SET p += {name: 'Ellen', livesIn: 'London'}
RETURN p.name, p.age, p.livesIn
----

The properties on the node are updated as follows by those provided in the map: the `name` property is updated from `Jane` to `Ellen`, the `age` property is left untouched, and the `livesIn` property is added.

.Result
[role="queryresult",options="header,footer",cols="3*<m"]
|===
| +p.name+ | +p.age+ | +p.livesIn+
| +"Ellen"+ | +20+ | +"London"+
3+d|Rows: 1 +
Nodes created: 1 +
Properties set: 4 +
Labels added: 1
|===

ifndef::nonhtmloutput[]
[subs="none"]
++++
<formalpara role="cypherconsole">
<title>Try this query live</title>
<para><database><![CDATA[
none
]]></database><command><![CDATA[
CREATE (a:Person {name: 'Jane', age: 20})
WITH a
MATCH (p:Person {name: 'Jane'})
SET p += {name: 'Ellen', livesIn: 'London'}
RETURN p.name, p.age, p.livesIn
]]></command></para></formalpara>
++++
endif::nonhtmloutput[]

See <<set-setting-properties-using-map>> for more details on using the property mutation operator `+=`.

[[query-operators-mathematical]]
== Mathematical operators

The mathematical operators comprise:

* addition: `+`
* subtraction or unary minus: `-`
* multiplication: `*`
* division: `/`
* modulo division: `%`
* exponentiation: `^`

[[syntax-using-the-exponentiation-operator]]
=== Using the exponentiation operator `^`


.Query
[source, cypher]
----
WITH 2 AS number, 3 AS exponent
RETURN number ^ exponent AS result
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +result+
| +8.0+
1+d|Rows: 1
|===

ifndef::nonhtmloutput[]
[subs="none"]
++++
<formalpara role="cypherconsole">
<title>Try this query live</title>
<para><database><![CDATA[
none
]]></database><command><![CDATA[
WITH 2 AS number, 3 AS exponent
RETURN number ^ exponent AS result
]]></command></para></formalpara>
++++
endif::nonhtmloutput[]

[[syntax-using-the-unary-minus-operator]]
=== Using the unary minus operator `-`


.Query
[source, cypher]
----
WITH -3 AS a, 4 AS b
RETURN b - a AS result
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +result+
| +7+
1+d|Rows: 1
|===

ifndef::nonhtmloutput[]
[subs="none"]
++++
<formalpara role="cypherconsole">
<title>Try this query live</title>
<para><database><![CDATA[
none
]]></database><command><![CDATA[
WITH -3 AS a, 4 AS b
RETURN b - a AS result
]]></command></para></formalpara>
++++
endif::nonhtmloutput[]

[[query-operators-comparison]]
== Comparison operators

The comparison operators comprise:

* equality: `=`
* inequality: `<>`
* less than: `<`
* greater than: `>`
* less than or equal to: `\<=`
* greater than or equal to: `>=`
* `IS NULL`
* `IS NOT NULL`

[[query-operator-comparison-string-specific]]
=== String-specific comparison operators comprise:

* `STARTS WITH`: perform case-sensitive prefix searching on strings
* `ENDS WITH`: perform case-sensitive suffix searching on strings
* `CONTAINS`: perform case-sensitive inclusion searching in strings

[[syntax-comparing-two-numbers]]
=== Comparing two numbers


.Query
[source, cypher]
----
WITH 4 AS one, 3 AS two
RETURN one > two AS result
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +result+
| +true+
1+d|Rows: 1
|===

ifndef::nonhtmloutput[]
[subs="none"]
++++
<formalpara role="cypherconsole">
<title>Try this query live</title>
<para><database><![CDATA[
none
]]></database><command><![CDATA[
WITH 4 AS one, 3 AS two
RETURN one > two AS result
]]></command></para></formalpara>
++++
endif::nonhtmloutput[]

See <<cypher-comparison>> for more details on the behavior of comparison operators, and <<query-where-ranges>> for more examples showing how these may be used.

[[syntax-using-starts-with-to-filter-names]]
=== Using `STARTS WITH` to filter names


.Query
[source, cypher]
----
WITH ['John', 'Mark', 'Jonathan', 'Bill'] AS somenames
UNWIND somenames AS names
WITH names AS candidate
WHERE candidate STARTS WITH 'Jo'
RETURN candidate
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +candidate+
| +"John"+
| +"Jonathan"+
1+d|Rows: 2
|===

ifndef::nonhtmloutput[]
[subs="none"]
++++
<formalpara role="cypherconsole">
<title>Try this query live</title>
<para><database><![CDATA[
none
]]></database><command><![CDATA[
WITH ['John', 'Mark', 'Jonathan', 'Bill'] AS somenames
UNWIND somenames AS names
WITH names AS candidate
WHERE candidate STARTS WITH 'Jo'
RETURN candidate
]]></command></para></formalpara>
++++
endif::nonhtmloutput[]

<<query-where-string>> contains more information regarding the string-specific comparison operators as well as additional examples illustrating the usage thereof.

include::../syntax/comparison.adoc[leveloffset=+1]

[[query-operators-boolean]]
== Boolean operators

The boolean operators -- also known as logical operators -- comprise:

* conjunction: `AND`
* disjunction: `OR`,
* exclusive disjunction: `XOR`
* negation: `NOT`

Here is the truth table for `AND`, `OR`, `XOR` and `NOT`.

[options="header", cols="^,^,^,^,^,^", width="85%"]
|===
|a | b | a `AND` b | a `OR` b | a `XOR` b | `NOT` a
|`false` | `false` | `false` | `false` | `false` | `true`
|`false` | `null` | `false` | `null` | `null` | `true`
|`false` | `true` | `false` | `true` | `true` | `true`
|`true` | `false` | `false` | `true` | `true` | `false`
|`true` | `null` | `null` | `true` | `null` | `false`
|`true` | `true` | `true` | `true` | `false` | `false`
|`null` | `false` | `false` | `null` | `null` | `null`
|`null` | `null` | `null` | `null` | `null` | `null`
|`null` | `true` | `null` | `true` | `null` | `null`
|===



[[syntax-using-boolean-operators-to-filter-numbers]]
=== Using boolean operators to filter numbers


.Query
[source, cypher]
----
WITH [2, 4, 7, 9, 12] AS numberlist
UNWIND numberlist AS number
WITH number
WHERE number = 4 OR (number > 6 AND number < 10)
RETURN number
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +number+
| +4+
| +7+
| +9+
1+d|Rows: 3
|===

ifndef::nonhtmloutput[]
[subs="none"]
++++
<formalpara role="cypherconsole">
<title>Try this query live</title>
<para><database><![CDATA[
none
]]></database><command><![CDATA[
WITH [2, 4, 7, 9, 12] AS numberlist
UNWIND numberlist AS number
WITH number
WHERE number = 4 OR (number > 6 AND number < 10)
RETURN number
]]></command></para></formalpara>
++++
endif::nonhtmloutput[]

[[query-operators-string]]
== String operators

The string operators comprise:

* concatenating strings: `+`
* matching a regular expression: `=~`

[[syntax-using-a-regular-expression-to-filter-words]]
=== Using a regular expression with `=~` to filter words


.Query
[source, cypher]
----
WITH ['mouse', 'chair', 'door', 'house'] AS wordlist
UNWIND wordlist AS word
WITH word
WHERE word =~ '.*ous.*'
RETURN word
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +word+
| +"mouse"+
| +"house"+
1+d|Rows: 2
|===

ifndef::nonhtmloutput[]
[subs="none"]
++++
<formalpara role="cypherconsole">
<title>Try this query live</title>
<para><database><![CDATA[
none
]]></database><command><![CDATA[
WITH ['mouse', 'chair', 'door', 'house'] AS wordlist
UNWIND wordlist AS word
WITH word
WHERE word =~ '.*ous.*'
RETURN word
]]></command></para></formalpara>
++++
endif::nonhtmloutput[]

Further information and examples regarding the use of regular expressions in filtering can be found in <<query-where-regex>>.
In addition, refer to <<query-operator-comparison-string-specific>> for details on string-specific comparison operators.

[[query-operators-temporal]]
== Temporal operators

Temporal operators comprise:

* adding a <<cypher-temporal-durations, _Duration_>> to either a <<cypher-temporal-instants, temporal instant>> or another _Duration_: `+`
* subtracting a _Duration_ from either a temporal instant or another _Duration_: `-`
* multiplying a _Duration_ with a number: `*`
* dividing a _Duration_ by a number: `/`

The following table shows -- for each combination of operation and operand type -- the type of the value returned from the application of each temporal operator:

[options="header"]
|===
| Operator | Left-hand operand | Right-hand operand | Type of result
| <<syntax-add-subtract-duration-to-temporal-instant, `+`>> | Temporal instant           | _Duration_                 | The type of the temporal instant
| <<syntax-add-subtract-duration-to-temporal-instant, `+`>> | _Duration_                 | Temporal instant           | The type of the temporal instant
| <<syntax-add-subtract-duration-to-temporal-instant, `-`>> | Temporal instant           | _Duration_                 | The type of the temporal instant
| <<syntax-add-subtract-duration-to-duration, `+`>>         | _Duration_                 | _Duration_                 | _Duration_
| <<syntax-add-subtract-duration-to-duration, `-`>>         | _Duration_                 | _Duration_                 | _Duration_
| <<syntax-multiply-divide-duration-number, `*`>>           | _Duration_                 | <<property-types, Number>> | _Duration_
| <<syntax-multiply-divide-duration-number, `*`>>           | <<property-types, Number>> | _Duration_                 | _Duration_
| <<syntax-multiply-divide-duration-number, `/`>>           | _Duration_                 | <<property-types, Number>> | _Duration_
|===

        

[[syntax-add-subtract-duration-to-temporal-instant]]
=== Adding and subtracting a _Duration_ to or from a temporal instant


.Query
[source, cypher]
----
WITH
  localdatetime({year:1984, month:10, day:11, hour:12, minute:31, second:14}) AS aDateTime,
  duration({years: 12, nanoseconds: 2}) AS aDuration
RETURN aDateTime + aDuration, aDateTime - aDuration
----

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===
| +aDateTime + aDuration+ | +aDateTime - aDuration+
| +1996-10-11T12:31:14.000000002+ | +1972-10-11T12:31:13.999999998+
2+d|Rows: 1
|===

ifndef::nonhtmloutput[]
[subs="none"]
++++
<formalpara role="cypherconsole">
<title>Try this query live</title>
<para><database><![CDATA[
none
]]></database><command><![CDATA[
WITH
  localdatetime({year:1984, month:10, day:11, hour:12, minute:31, second:14}) AS aDateTime,
  duration({years: 12, nanoseconds: 2}) AS aDuration
RETURN aDateTime + aDuration, aDateTime - aDuration
]]></command></para></formalpara>
++++
endif::nonhtmloutput[]

<<cypher-temporal-duration-component, Components of a _Duration_>> that do not apply to the temporal instant are ignored.
For example, when adding a _Duration_ to a _Date_, the _hours_, _minutes_, _seconds_ and _nanoseconds_ of the _Duration_ are ignored (_Time_ behaves in an analogous manner):
          


.Query
[source, cypher]
----
WITH
  date({year:1984, month:10, day:11}) AS aDate,
  duration({years: 12, nanoseconds: 2}) AS aDuration
RETURN aDate + aDuration, aDate - aDuration
----

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===
| +aDate + aDuration+ | +aDate - aDuration+
| +1996-10-11+ | +1972-10-11+
2+d|Rows: 1
|===

ifndef::nonhtmloutput[]
[subs="none"]
++++
<formalpara role="cypherconsole">
<title>Try this query live</title>
<para><database><![CDATA[
none
]]></database><command><![CDATA[
WITH
  date({year:1984, month:10, day:11}) AS aDate,
  duration({years: 12, nanoseconds: 2}) AS aDuration
RETURN aDate + aDuration, aDate - aDuration
]]></command></para></formalpara>
++++
endif::nonhtmloutput[]

Adding two durations to a temporal instant is not an associative operation.
This is because non-existing dates are truncated to the nearest existing date:


.Query
[source, cypher]
----
RETURN
  (date("2011-01-31") + duration("P1M")) + duration("P12M") AS date1,
  date("2011-01-31") + (duration("P1M") + duration("P12M")) AS date2
----

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===
| +date1+ | +date2+
| +2012-02-28+ | +2012-02-29+
2+d|Rows: 1
|===

ifndef::nonhtmloutput[]
[subs="none"]
++++
<formalpara role="cypherconsole">
<title>Try this query live</title>
<para><database><![CDATA[
none
]]></database><command><![CDATA[
RETURN
  (date("2011-01-31") + duration("P1M")) + duration("P12M") AS date1,
  date("2011-01-31") + (duration("P1M") + duration("P12M")) AS date2
]]></command></para></formalpara>
++++
endif::nonhtmloutput[]

[[syntax-add-subtract-duration-to-duration]]
=== Adding and subtracting a _Duration_ to or from another _Duration_


.Query
[source, cypher]
----
WITH
  duration({years: 12, months: 5, days: 14, hours: 16, minutes: 12, seconds: 70, nanoseconds: 1}) as duration1,
  duration({months:1, days: -14, hours: 16, minutes: -12, seconds: 70}) AS duration2
RETURN duration1, duration2, duration1 + duration2, duration1 - duration2
----

.Result
[role="queryresult",options="header,footer",cols="4*<m"]
|===
| +duration1+ | +duration2+ | +duration1 + duration2+ | +duration1 - duration2+
| +P12Y5M14DT16H13M10.000000001S+ | +P1M-14DT15H49M10S+ | +P12Y6MT32H2M20.000000001S+ | +P12Y4M28DT24M0.000000001S+
4+d|Rows: 1
|===

ifndef::nonhtmloutput[]
[subs="none"]
++++
<formalpara role="cypherconsole">
<title>Try this query live</title>
<para><database><![CDATA[
none
]]></database><command><![CDATA[
WITH
  duration({years: 12, months: 5, days: 14, hours: 16, minutes: 12, seconds: 70, nanoseconds: 1}) as duration1,
  duration({months:1, days: -14, hours: 16, minutes: -12, seconds: 70}) AS duration2
RETURN duration1, duration2, duration1 + duration2, duration1 - duration2
]]></command></para></formalpara>
++++
endif::nonhtmloutput[]

[[syntax-multiply-divide-duration-number]]
=== Multiplying and dividing a _Duration_ with or by a number

These operations are interpreted simply as component-wise operations with overflow to smaller units based on an average length of units in the case of division (and multiplication with fractions).


.Query
[source, cypher]
----
WITH duration({days: 14, minutes: 12, seconds: 70, nanoseconds: 1}) AS aDuration
RETURN aDuration, aDuration * 2, aDuration / 3
----

.Result
[role="queryresult",options="header,footer",cols="3*<m"]
|===
| +aDuration+ | +aDuration * 2+ | +aDuration / 3+
| +P14DT13M10.000000001S+ | +P28DT26M20.000000002S+ | +P4DT16H4M23.333333333S+
3+d|Rows: 1
|===

ifndef::nonhtmloutput[]
[subs="none"]
++++
<formalpara role="cypherconsole">
<title>Try this query live</title>
<para><database><![CDATA[
none
]]></database><command><![CDATA[
WITH duration({days: 14, minutes: 12, seconds: 70, nanoseconds: 1}) AS aDuration
RETURN aDuration, aDuration * 2, aDuration / 3
]]></command></para></formalpara>
++++
endif::nonhtmloutput[]

[[query-operators-map]]
== Map operators

The map operators comprise:

* statically access the value of a map by key using the dot operator: `.`
* dynamically access the value of a map by key using the subscript operator: `[]`


[NOTE]
====
The behavior of the `[]` operator with respect to `null` is detailed in <<cypher-null-bracket-operator>>.


====

[[syntax-accessing-the-value-of-a-nested-map]]
=== Statically accessing the value of a nested map by key using the `.` operator


.Query
[source, cypher]
----
WITH {person: {name: 'Anne', age: 25}} AS p
RETURN  p.person.name
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +p.person.name+
| +"Anne"+
1+d|Rows: 1
|===

ifndef::nonhtmloutput[]
[subs="none"]
++++
<formalpara role="cypherconsole">
<title>Try this query live</title>
<para><database><![CDATA[
none
]]></database><command><![CDATA[
WITH {person: {name: 'Anne', age: 25}} AS p
RETURN  p.person.name
]]></command></para></formalpara>
++++
endif::nonhtmloutput[]

[[syntax-accessing-dynamic-map-parameter]]
=== Dynamically accessing the value of a map by key using the `[]` operator and a parameter

A parameter may be used to specify the key of the value to access:


.Parameters
[source,javascript]
----
{
  "myKey" : "name"
}
----


.Query
[source, cypher]
----
WITH {name: 'Anne', age: 25} AS a
RETURN a[$myKey] AS result
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +result+
| +"Anne"+
1+d|Rows: 1
|===

ifndef::nonhtmloutput[]
[subs="none"]
++++
<formalpara role="cypherconsole">
<title>Try this query live</title>
<para><database><![CDATA[
none
]]></database><command><![CDATA[
WITH {name: 'Anne', age: 25} AS a
RETURN a[$myKey] AS result
]]></command></para></formalpara>
++++
endif::nonhtmloutput[]

More details on maps can be found in <<cypher-maps>>.

[[query-operators-list]]
== List operators

The list operators comprise:

* concatenating lists `l~1~` and `l~2~`: `[l~1~] + [l~2~]`
* checking if an element `e` exists in a list `l`: `e IN [l]`
* dynamically accessing an element(s) in a list using the subscript operator: `[]`

[NOTE]
====
The behavior of the `IN` and `[]` operators with respect to `null` is detailed <<cypher-working-with-null, here>>.


====

[[syntax-concatenating-two-lists]]
=== Concatenating two lists using `+`


.Query
[source, cypher]
----
RETURN [1,2,3,4,5] + [6,7] AS myList
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +myList+
| +[1,2,3,4,5,6,7]+
1+d|Rows: 1
|===

ifndef::nonhtmloutput[]
[subs="none"]
++++
<formalpara role="cypherconsole">
<title>Try this query live</title>
<para><database><![CDATA[
none
]]></database><command><![CDATA[
RETURN [1,2,3,4,5] + [6,7] AS myList
]]></command></para></formalpara>
++++
endif::nonhtmloutput[]

[[syntax-using-in-to-check-if-a-number-is-in-a-list]]
=== Using `IN` to check if a number is in a list


.Query
[source, cypher]
----
WITH [2, 3, 4, 5] AS numberlist
UNWIND numberlist AS number
WITH number
WHERE number IN [2, 3, 8]
RETURN number
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +number+
| +2+
| +3+
1+d|Rows: 2
|===

ifndef::nonhtmloutput[]
[subs="none"]
++++
<formalpara role="cypherconsole">
<title>Try this query live</title>
<para><database><![CDATA[
none
]]></database><command><![CDATA[
WITH [2, 3, 4, 5] AS numberlist
UNWIND numberlist AS number
WITH number
WHERE number IN [2, 3, 8]
RETURN number
]]></command></para></formalpara>
++++
endif::nonhtmloutput[]

[[syntax-using-in-for-more-complex-list-membership-operations]]
=== Using `IN` for more complex list membership operations

The general rule is that the `IN` operator will evaluate to `true` if the list given as the right-hand operand contains an element which has the same _type and contents (or value)_ as the left-hand operand.
Lists are only comparable to other lists, and elements of a list `innerList` are compared pairwise in ascending order from the first element in `innerList` to the last element in `innerList`.

The following query checks whether or not the list `[2, 1]` is an element of the list `[1, [2, 1], 3]`:


.Query
[source, cypher]
----
RETURN [2, 1] IN [1, [2, 1], 3] AS inList
----

The query evaluates to `true` as the right-hand list contains, as an element, the list `[1, 2]` which is of the same type (a list) and contains the same contents (the numbers `2` and `1` in the given order) as the left-hand operand.
If the left-hand operator had been `[1, 2]` instead of `[2, 1]`, the query would have returned `false`.
            

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +inList+
| +true+
1+d|Rows: 1
|===

ifndef::nonhtmloutput[]
[subs="none"]
++++
<formalpara role="cypherconsole">
<title>Try this query live</title>
<para><database><![CDATA[
none
]]></database><command><![CDATA[
RETURN [2, 1] IN [1, [2, 1], 3] AS inList
]]></command></para></formalpara>
++++
endif::nonhtmloutput[]

At first glance, the contents of the left-hand operand and the right-hand operand _appear_ to be the same in the following query:


.Query
[source, cypher]
----
RETURN [1, 2] IN [1, 2] AS inList
----

However, `IN` evaluates to `false` as the right-hand operand does not contain an element that is of the same _type_ -- i.e. a _list_ -- as the left-hand-operand.

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +inList+
| +false+
1+d|Rows: 1
|===

ifndef::nonhtmloutput[]
[subs="none"]
++++
<formalpara role="cypherconsole">
<title>Try this query live</title>
<para><database><![CDATA[
none
]]></database><command><![CDATA[
RETURN [1, 2] IN [1, 2] AS inList
]]></command></para></formalpara>
++++
endif::nonhtmloutput[]

The following query can be used to ascertain whether or not a list -- obtained from, say, the <<functions-labels, labels()>> function -- contains at least one element that is also present in another list:

[source, cypher]
----
MATCH (n)
WHERE size([label IN labels(n) WHERE label IN ['Person', 'Employee'] | 1]) > 0
RETURN count(n)
----


As long as `labels(n)` returns either `Person` or `Employee` (or both), the query will return a value greater than zero.

[[syntax-accessing-elements-in-a-list]]
=== Accessing elements in a list using the `[]` operator


.Query
[source, cypher]
----
WITH ['Anne', 'John', 'Bill', 'Diane', 'Eve'] AS names
RETURN names[1..3] AS result
----

The square brackets will extract the elements from the start index `1`, and up to (but excluding) the end index `3`.

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +result+
| +["John","Bill"]+
1+d|Rows: 1
|===

ifndef::nonhtmloutput[]
[subs="none"]
++++
<formalpara role="cypherconsole">
<title>Try this query live</title>
<para><database><![CDATA[
none
]]></database><command><![CDATA[
WITH ['Anne', 'John', 'Bill', 'Diane', 'Eve'] AS names
RETURN names[1..3] AS result
]]></command></para></formalpara>
++++
endif::nonhtmloutput[]

[[syntax-accessing-element-in-a-list-parameter]]
=== Dynamically accessing an element in a list using the `[]` operator and a parameter

A parameter may be used to specify the index of the element to access:


.Parameters
[source,javascript]
----
{
  "myIndex" : 1
}
----


.Query
[source, cypher]
----
WITH ['Anne', 'John', 'Bill', 'Diane', 'Eve'] AS names
RETURN names[$myIndex] AS result
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +result+
| +"John"+
1+d|Rows: 1
|===

ifndef::nonhtmloutput[]
[subs="none"]
++++
<formalpara role="cypherconsole">
<title>Try this query live</title>
<para><database><![CDATA[
none
]]></database><command><![CDATA[
WITH ['Anne', 'John', 'Bill', 'Diane', 'Eve'] AS names
RETURN names[$myIndex] AS result
]]></command></para></formalpara>
++++
endif::nonhtmloutput[]

[[syntax-using-in-with-nested-list-subscripting]]
=== Using `IN` with `[]` on a nested list

`IN` can be used in conjunction with `[]` to test whether an element exists in a nested list:


.Parameters
[source,javascript]
----
{
  "myIndex" : 1
}
----


.Query
[source, cypher]
----
WITH [[1, 2, 3]] AS l
RETURN 3 IN l[0] AS result
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +result+
| +true+
1+d|Rows: 1
|===

ifndef::nonhtmloutput[]
[subs="none"]
++++
<formalpara role="cypherconsole">
<title>Try this query live</title>
<para><database><![CDATA[
none
]]></database><command><![CDATA[
WITH [[1, 2, 3]] AS l
RETURN 3 IN l[0] AS result
]]></command></para></formalpara>
++++
endif::nonhtmloutput[]

More details on lists can be found in <<cypher-lists-general>>.

