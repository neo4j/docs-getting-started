= Updating the graph
:tags: cypher, queries, graph-queries, insert-create, update, delete, merge
:description: This guide explains how to update data that already exists in a graph, using Cypher.
:page-newsletter: true
:page-ad-overline-link: https://graphacademy.neo4j.com/?ref=guides
:page-ad-overline: Neo4j GraphAcademy
:page-ad-title: Cypher Fundamentals
:page-ad-description: Learn Cypher in this free, hands-on course
:page-ad-link: https://graphacademy.neo4j.com/?ref=guides
:page-ad-underline-role: button
:page-ad-underline: Learn more

This tutorial shows how to update information in the graph by changing, removing, and adding nodes, relationships, and properties.
It also addresses how to avoid duplication and how to keep track of changes by adding new properties to the graph.

== Create the dataset

After you link:https://neo4j.com/product/auradb/?ref=docs-nav-get-started[create a free Aura instance], use the "Connect" button and select "Query". 
In the Cypher editor, copy and paste the following Cypher and execute the query:

[source, cypher]
--
CREATE (diana:Person {name: "Diana"})
CREATE (melissa:Person {name: "Melissa", twitter: "@melissa"})
CREATE (dan:Person {name: "Dan", twitter: "@dan", yearsExperience: 6})
CREATE (sally:Person {name: "Sally", yearsExperience: 4})
CREATE (john:Person {name: "John", yearsExperience: 5})
CREATE (jennifer:Person {name: "Jennifer", twitter: "@jennifer", yearsExperience: 5})
CREATE (joe:Person {name: "Joe"})
CREATE (mark:Person {name: "Mark", twitter: "@mark"})
CREATE (ann:Person {name: "Ann"})
CREATE (xyz:Company {name: "XYZ"})
CREATE (x:Company {name: "Company X"})
CREATE (a:Company {name: "Company A"})
CREATE (Neo4j:Company {name: "Neo4j"})
CREATE (abc:Company {name: "ABC"})
CREATE (query:Technology {type: "Query Languages"})
CREATE (etl:Technology {type: "Data ETL"})
CREATE (integrations:Technology {type: "Integrations"})
CREATE (graphs:Technology {type: "Graphs"})
CREATE (dev:Technology {type: "Application Development"})
CREATE (java:Technology {type: "Java"})
CREATE (diana)-[:LIKES]->(query)
CREATE (melissa)-[:LIKES]->(query)
CREATE (dan)-[:LIKES]->(etl)<-[:LIKES]-(melissa)
CREATE (xyz)<-[:WORKS_FOR]-(sally)-[:LIKES]->(integrations)<-[:LIKES]-(dan)
CREATE (sally)<-[:IS_FRIENDS_WITH]-(john)-[:LIKES]->(java)
CREATE (john)<-[:IS_FRIENDS_WITH]-(jennifer)-[:LIKES]->(java)
CREATE (john)-[:WORKS_FOR]->(xyz)
CREATE (sally)<-[:IS_FRIENDS_WITH]-(jennifer)-[:IS_FRIENDS_WITH]->(melissa)
CREATE (joe)-[:LIKES]->(query)
CREATE (x)<-[:WORKS_FOR]-(diana)<-[:IS_FRIENDS_WITH]-(joe)-[:IS_FRIENDS_WITH]->(mark)-[:LIKES]->(graphs)<-[:LIKES]-(jennifer)-[:WORKS_FOR]->(Neo4j)
CREATE (ann)<-[:IS_FRIENDS_WITH]-(jennifer)-[:IS_FRIENDS_WITH]->(mark)
CREATE (john)-[:LIKES]->(dev)<-[:LIKES]-(ann)-[:IS_FRIENDS_WITH]->(dan)-[:WORKS_FOR]->(abc)
CREATE (ann)-[:WORKS_FOR]->(abc)
CREATE (a)<-[:WORKS_FOR]-(melissa)-[:LIKES]->(graphs)<-[:LIKES]-(diana)
--

You should now have a graph with 20 nodes, 31 relationships, 28 properties, and 20 labels.

== Update a node

This is the query you need to update Jennifer's node with information about her birthday:

[source, cypher]
--
MATCH (p:Person {name: 'Jennifer'})
SET p.birthdate = date('1980-01-01')
RETURN p
--

What this query does is:

. Find Jennifer's already existing node with the `MATCH` clause.
. Use `SET` to create the new property `birthday` (with syntax `variable.property`) and set its value.
. Use `RETURN` to retrieve Jennifer's node and ensure that the information was updated correctly.

This is the result:

[role="queryresult",options="header,footer",cols="1*<m"]
|===
| p

| (:Person {twitter: "@jennifer", birthdate: 1980-01-01, name: "Jennifer", yearsExperience: 5})

1+d|Rows: 1
|===

== Update a property

To change Jennifer's birthdate, you can use the same query from the previous section to find Jennifer's node again, and then put a different date in the `SET` clause:

[source, cypher]
--
MATCH (p:Person {name: 'Jennifer'})
SET p.birthdate = date('1980-01-02')
RETURN p
--

This is the result:

[role="queryresult",options="header,footer",cols="1*<m"]
|===
| p

| (:Person {twitter: "@jennifer", birthdate: 1980-01-02, name: "Jennifer", yearsExperience: 5})

1+d|Rows: 1
|===

== Delete properties

Neo4j doesn't store null values, which means that if you set a property value as `null`, it will be automatically removed from your graph.
You can do this through two methods:

.Using the `REMOVE` clause
[source,cypher]
--
MATCH (n:Person {name: 'Jennifer'})
REMOVE n.birthdate
--

.Using the `SET` clause
[source,cypher]
--
MATCH (n:Person {name: 'Jennifer'})
SET n.birthdate = null
--

In both options, the result will be "Set 1 property", and when you `MATCH` it, you will see that the result shows the value `null`.
This means that the property doesn't hold any value and doesn't exist in the graph anymore.

== Update a relationship

To update Jennifer's `WORKS_FOR` relationship with a `Company` node to include the year that she started working there, you can use a similar syntax as the previous steps:

[source, cypher]
----
MATCH (:Person {name: 'Jennifer'})-[rel:WORKS_FOR]-(:Company {name: 'Neo4j'})
SET rel.startYear = date({year: 2018})
RETURN rel
----

This is the result:

[role="queryresult",options="header,footer",cols="1*<m"]
|===
| rel

| [:WORKS_FOR {startYear: 2018-01-01}]

1+d|Rows: 1
|===

== Delete a relationship

You can use the `DELETE` clause for deleting both relationships and nodes.
However, because Neo4j is ACID-compliant, you cannot delete a node if it still has relationships.

Therefore, if you want to xref:cypher/updating.adoc#_delete_a_node[delete a node], for example, Jennifer's node, you need to first delete the relationships appended to it:

[source,cypher]
--
MATCH (j:Person {name:"Jennifer"})-[r]-(n)
DELETE r
--

Note that the arrow is undirected because you want to delete *all* relationships connected to Jennifer, no matter what direction.

The result is the following message: "Deleted 8 relationships".

== Delete a node

Now that you have deleted all relationships in xref:cypher/updating.adoc#_delete_a_relationship[the previous step], you can delete Jennifer's node in itself by using the following query:

[source,cypher]
--
MATCH (j:Person {name:"Jennifer"})
DELETE j
--

== Delete a node and its relationships

To repeat the same two previous steps with another node, you don't need to write two statements.
You can do this in one single query, but with some modifications that follow Neo4j's ACID compliance.

This time, you need to use `DETACH DELETE` to remove any existing relationships Mark's node *and* delete the node itself:

[source, cypher]
--
MATCH (m:Person {name: 'Mark'})
DETACH DELETE m
--

The result is the following message: "Deleted 1 node, deleted 2 relationships".

== Add new data

In order to avoid duplication, instead of using the `CREATE` clause from the first step, use `MERGE`.

`MERGE` does a "select-or-insert" operation that first checks if the data exists in the database.
If it exists, then Cypher returns it as it is or makes any specified updates to the existing node or relationship.
If the data don't exist, then Cypher creates it with the information you specify.

Since you removed Mark's node in xref:cypher/updating.adoc#_delete_a_node_and_its_relationships[the previous step], Cypher will not find an existing match.
It will create a new node with the `name` property set to 'Mark' as value:

[source, cypher]
----
MERGE (mark:Person {name: 'Mark'})
RETURN mark
----

This is the result:

[role="queryresult",options="header,footer",cols="1*<m"]
|===
| mark

| (:Person {twitter: "@mark", name: "Mark"})

1+d|Rows: 1
|===

If you run the same statement again, Cypher will find an existing node and make no changes.

Now, if you want to create a new relationship, you should first use `MATCH` and then `MERGE` in order to avoid duplication.
For example, if you write the query like this:

[source, cypher]
--
MERGE (j:Person {name: 'Jennifer'})-[r:IS_FRIENDS_WITH]->(m:Person {name: 'Mark'})
RETURN j, r, m
--

Cypher won't find this specific pattern, so it will create it from scratch, which means both nodes and the relationship between them will be created.
Since the relationship didn't exist previously, it is Jennifer and Mark's nodes that will be duplicated.

[TIP]
====
If you ran this statement and created the duplication, you can go back to the previous steps where you saw xref:cypher/updating.adoc#_delete_a_node_and_its_relationships[how to delete a node and its relationships].
====

To avoid this scenario, you should write the statement like this instead:

[source, cypher]
----
MATCH (j:Person {name: 'Jennifer'})
MATCH (m:Person {name: 'Mark'})
MERGE (j)-[r:IS_FRIENDS_WITH]->(m)
RETURN j, r, m
----

`MATCH` is used here to find both Mark and Jennifer's node before using `MERGE` to find or create the relationship between them.
Since this relationship didn't exist anymore after you xref:cypher/updating.adoc#_delete_a_node_and_its_relationships[deleted Mark's node and appended relationships], the result will be a newly created relationship:

image::jennifer-friends-mark.svg[Jennifer and Mark's nodes are connected through a is friends with relationship,width=400,role=popup]

== Track changes

You can use the `MERGE` clause both for avoiding duplicates and also for keeping track of changes done to the graph.
For example, you can combine it with link:https://neo4j.com/docs/cypher-manual/current/clauses/merge/#query-merge-on-create-on-match[`ON CREATE` or `ON MATCH`] with the `SET` keyword to handle these situations:

[source, cypher]
----
MERGE (m:Person {name: 'Mark'})-[r:IS_FRIENDS_WITH]-(j:Person {name:'Jennifer'})
  ON CREATE SET r.since = date('2018-03-01')
  ON MATCH SET r.updated = date()
RETURN m, r, j
----

Now you have the new properties `since` and `updated` added to the `IS_FRIENDS_WITH` relationship.
They give information about how long Jennifer and Mark have been friends and when this information was updated to the graph.

Supposing you made this change to the graph on November 24th, 2025, you will get the following result:

[role="queryresult",options="header,footer",cols="3*<m"]
|===
| m | r | j

| (:Person {twitter: "@mark", name: "Mark"})
| [:IS_FRIENDS_WITH {updated: 2025-11-24}]
| (:Person {twitter: "@jennifer", name: "Jennifer", yearsExperience: 5})

3+d|Rows: 3
|===

== Conclusion

In this tutorial, you have seen how to update the information in your graph and avoid issues such as duplication or lack of information about when the changes were made.
You have used different Cypher clauses including `MATCH`, `MERGE`, `DELETE`, and more.

== Keep learning

If you want to learn more about how to update your graph and find the best way to model your data, refer to:

* xref:data-modeling/index.adoc[*Data modeling*] -> A complete section on how to get started with data modeling through tutorials and reference material.