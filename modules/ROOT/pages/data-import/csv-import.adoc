[[csv-import]]
= Tutorial: Import CSV data using `LOAD CSV`
:description: This tutorial shows how to use the command LOAD CSV to import small to medium datasets in CSV format into Neo4j using Aura and on-premise deployments.
:page-pagination:

Neo4j supports xref:data-import/index.adoc#_methods_comparison[different methods] of importing data.
Which method to use depends on factors like dataset size, link:{docs-home}/deployment-method[deployment method], and familiarity with Cypher.
This tutorial shows how to import CSV data into Neo4j using link:https://neo4j.com/docs/cypher-manual/current/clauses/load-csv/[`LOAD CSV`] command.

[TIP]
====
For a more hands-on experience, enroll in the GraphAcademy course link:https://graphacademy.neo4j.com/courses/importing-cypher/[Importing CSV data into Neo4j].
To import larger CSV files into Neo4j, see the tutorial for link:https://neo4j.com/docs/operations-manual/current/tutorial/neo4j-admin-import/[Neo4j-admin import].
====

== Loading the file

Before loading the file, you need to first create an link:https://neo4j.com/product/auradb/[Aura instance] or choose a link:{docs-home}/deployment-options[deployment of your choice].
Then, you can load the file using `LOAD CSV` using the following command:

[source,cypher]
--
LOAD CSV [WITH HEADERS] FROM url [AS alias] [FIELDTERMINATOR char]
--

In this tutorial, you will use a link to load the CSV file:

[source,cypher]
--
LOAD CSV WITH HEADERS 
FROM 'https://data.neo4j.com/importing-cypher/people.csv' AS row
RETURN row
--

This is the content of the example `people.csv` file:

[source,csv]
--
personId,name,birthYear
23945,Gerard Pires,1942
553509,Helen Reddy,1941
113934,Susan Flannery,1939
--

Note that the `FIELDTERMINATOR` wasnâ€™t specified in the `LOAD CSV` clause because the default value is a comma. 
If the field terminator was a pipe character, then you would need to include the field terminator, for example:

[source,cypher]
--
LOAD CSV WITH HEADERS
FROM 'https://data.neo4j.com/importing-cypher/people.csv'
as row FIELDTERMINATOR '|'
RETURN row
--

== Check the imported data

You may want to verify that your data has been imported correctly.
These are some things you can do to ensure that the imported data is accurate:

. Count the number of rows in the CSV file and compare it to the number of rows returned by the `LOAD CSV` clause by using the link:https://neo4j.com/docs/cypher-manual/current/subqueries/count/[`COUNT`] function in a new query:
+
[source,cypher]
--
LOAD CSV WITH HEADERS
FROM 'https://data.neo4j.com/importing-cypher/people.csv'
AS row
RETURN count(row)
--
+

. Make sure header names match those in the CSV file.

. Search for typos in the data and in the queries.

== Filtering loaded data

After inspecting the data, you may only want to view or load a subset of the data in the CSV file. 
You can filter what you view (or load), for example, by choosing to view only those who were born in 1942:

[source,cypher]
--
LOAD CSV WITH HEADERS
FROM 'https://data.neo4j.com/importing-cypher/people.csv' 
AS row
WITH row WHERE row.birthYear = '1942'
RETURN row
--

The result is:

[options=header,cols=1]
|===

|row
|{
    +
  "birthYear": "1942", 
  +
  "name": "Gerard Pires",
  +
  "personId": "23945"
  +
}
|===

Note that in the query, you needed to refer to the year of 1942 as a string (encased by a single quote `'`).
This is because xref:data-import/csv-files.adoc#_data_types[`LOAD CSV` reads all values as a string].
In the next step, you will learn how to convert the values to their respective types.

== Convert data types

In the *people.csv* file, you have data such as the birth year and the ID number for each person.
`LOAD CSV` only reads values as string.
For better processing of the data, you can convert these values into temporal and numerical types.

`personId` can be turned into an interger by using the `toInteger()` function, and `birthYear` can be turned into a date format by using the  the `date()` function:

[source, cypher]
----
LOAD CSV WITH HEADERS
FROM 'https://data.neo4j.com/importing-cypher/people.csv'
AS row
WITH toInteger(row.personId) AS personId, date(row.birthYear) AS birthYear
RETURN personId, birthYear
----

.Result
[options=header,cols="1,1"]
|===

| personId
| birthYear

|23945	
|"1942-01-01"

|553509	
|"1941-01-01"

|113934	
|"1939-01-01"

|===

Note that despite of `birthYear` having information only about the year, when converted into a `date` type, Cypher automatically defaults to January 1st of the specified year.

== Model your data

[TIP]
====
Follow the xref:data-modeling/tutorial-data-modeling.adoc[Tutorial: Create a graph data model] to learn more.
====

Before you import data into Neo4j, there is no structure, only the dataset itself.
As you import data, however, you create the xref:data-modeling/index.adoc[graph data model].

Now that you have imported the data, you need to organize it into a graph.
You can use the `CREATE` clause to create your nodes and relationships, but then you may end up with duplicate entities in your graph.

The `CREATE` clause pays no attention to whether an identical element already exists, so unless your data is perfectly clean of duplications, it is better to use `MERGE`.
The `MERGE` clause tries to find a matching entity first, and only creates a new one if none is found.

[source, cypher]
--
LOAD CSV WITH HEADERS
FROM 'https://data.neo4j.com/importing-cypher/people.csv'
AS row
WITH toInteger(row.personId) AS personId, row.name AS Person, date(row.birthYear) AS birthYear
MERGE (p:Person {personId: personId})
  SET p.name = Person, p.birthYear = birthYear
RETURN p
--

This results in the following graph:

image::import-peoplecsv.svg[The graph resulted from the importing of the people csv file,role=popup,width=600]

== Keep learning

Regardless of where your data comes from, it is likely that it needs some preparation before it is ready to be imported.
Here is some complementary material you can refer to as you continue learning:

* xref:data-import/csv-files.adoc[*Working with CSV files*]: read about the structure of a CSV file and understand how data is organized.
* xref:data-import/csv-files.adoc#_cleaning_up[*Cleaning up CSV files*]: see how to use the `LOAD CSV` command to clean up the file while importing.
* xref:data-import/csv-files.adoc#_optimization[*Optimization*]: improve performance when working with large amounts of data or complex loading.